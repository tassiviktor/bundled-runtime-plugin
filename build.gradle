plugins {
    id 'java-gradle-plugin'
    id 'maven-publish'
    id 'com.gradle.plugin-publish' version '1.2.1'
}

group = 'hu.tassiviktor.gradle.plugins'
version = '1.0.2'

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

dependencies {

    testImplementation gradleTestKit()
    testImplementation platform('org.junit:junit-bom:5.10.2')
    testImplementation 'org.junit.jupiter:junit-jupiter'
}

test {
    useJUnitPlatform()
}

gradlePlugin {
    website = 'https://github.com/tassiviktor/bundled-runtime-plugin'  // kötelező meta
    vcsUrl  = 'https://github.com/tassiviktor/bundled-runtime-plugin.git'

    plugins {
        bundledRuntime {
            id = 'hu.tassiviktor.gradle.plugins.bundled-runtime'
            implementationClass = 'hu.tassiviktor.gradle.plugins.bundledruntime.BundledRuntimePlugin'
            displayName = "Bundled Runtime Plugin"
            description = """
Gradle plugin that builds a self-contained, Docker-ready runtime bundle for Java and Spring Boot applications.
It automatically detects required JDK modules, creates a minimized jlink runtime image,
packs your app JARs and launcher scripts, and optionally produces a distributable ZIP.
Perfect for lightweight container images or standalone deployments.
""".stripIndent().trim()
            tags.set([
                    "spring-boot", "spring", "jlink", "java", "runtime", "bundle",
                    "docker", "distribution", "fatjar", "packaging", "deployment"
            ])
        }
    }
}

publishing {
    publications {
        def p = findByName('mavenJava')
        if (p != null) {
            remove(p)
        }
    }
}

tasks.withType(Javadoc).configureEach {
    options.addStringOption('Xdoclint:none', '-quiet')
}